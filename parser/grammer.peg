{
package parser

import (
    "fmt"

    "github.com/maniartech/uexl_go/ast"
)

var nodeBuffer []ast.Node
var pType string
var pTypeBuffer []string
}

/////////////////// Expressions //////////////////////

Input <- expr:Expr EOF {
  cntCodeBlocks++
  return expr, nil
} / expr:PipeExpr EOF {
  cntCodeBlocks++
  return expr, nil
}

Expr <- _ first:Term rest:( _ LogOp _ Term )* _ {
  cntCodeBlocks++

  // Expr:
  return ast.ParseExpression(string(c.text), first, rest, c.pos.offset, c.pos.line, c.pos.col)
}

Term <- first:Bit rest:( _ CmpOp _ Bit )* {
  cntCodeBlocks++

  // fmt.Println("Term:", first, rest)
  return ast.ParseExpression(string(c.text), first, rest, c.pos.offset, c.pos.line, c.pos.col)
}

Bit <- first:Shift rest:( _ BitOp _ Shift )* {
  cntCodeBlocks++

  // fmt.Println("Bit:", first, rest)
  return ast.ParseExpression(string(c.text), first, rest, c.pos.offset, c.pos.line, c.pos.col)
}

Shift <- first:Sum rest: ( _ ShiftOp _ Sum )* {
  cntCodeBlocks++

  // fmt.Println("Shift:", first, rest)
  return ast.ParseExpression(string(c.text), first, rest, c.pos.offset, c.pos.line, c.pos.col)
}

Sum <- first:Mul rest:( _ AddOp _ Mul )* {
  cntCodeBlocks++

  // fmt.Println("Sum:", first, rest)
  return ast.ParseExpression(string(c.text), first, rest, c.pos.offset, c.pos.line, c.pos.col)
}

Mul <- first:Mod rest:( _ MulOp _ Mod )* {
  cntCodeBlocks++

  // fmt.Println("Mul:", first, rest)
  return ast.ParseExpression(string(c.text), first, rest, c.pos.offset, c.pos.line, c.pos.col)
}

Mod <- first:Factor rest:( _ ModOp _ Factor )* {
  cntCodeBlocks++

  // fmt.Println("Mod:", first, rest)
  return ast.ParseExpression(string(c.text), first, rest, c.pos.offset, c.pos.line, c.pos.col)
}

Factor <- '(' expr:Expr ')' {
  cntCodeBlocks++
  return expr, nil
} / value:Value {
  cntCodeBlocks++
  return value, nil
} / pipe:Pipe {
  cntCodeBlocks++
  return pipe, nil
} 

PipeExpr <- expr:Expr pipe:Pipe+ {
  cntCodeBlocks++
  pTypeBufferTmp := pTypeBuffer
  pTypeBuffer = nil
  nodeBufferTmp := nodeBuffer
  nodeBuffer = nil
  return ast.NewPipeNode(string(c.text), pTypeBufferTmp, expr.(ast.Node), nodeBufferTmp, c.pos.offset, c.pos.line, c.pos.col)
}

PipeType <- [a-z]+

Pipe <- '|' pipeType: PipeType* ':' expr:Expr {
  nodeBuffer = append(nodeBuffer, expr.(ast.Node))
  if len(pipeType.([]interface{})) == 0 {
    pType = "pipe"
  } else {
    pType = resolveAscii(pipeType)
  }
  pTypeBuffer = append(pTypeBuffer, pType)
  return nil, nil
}

//////////////// Operators Definitions //////////////////////

AddOp <- ( '+' / '-' ) {
  cntCodeBlocks++
  return string(c.text), nil
}

MulOp <- ( '*' / '/' ) {
  cntCodeBlocks++
  return string(c.text), nil
}

ModOp <- ( "//" ) {
  cntCodeBlocks++
  return string(c.text), nil
}

CmpOp <- ( "==" / "!=" / "<=" / ">=" / '<' / '>' ) {
  cntCodeBlocks++
  return string(c.text), nil
}

BitOp <- ( '&' / '|' / '^' ) {
  cntCodeBlocks++
  return string(c.text), nil
}

LogOp <- ( "&&" / "||" ) {
  cntCodeBlocks++
  return string(c.text), nil
}

ShiftOp <- ( "<<" / ">>") {
  cntCodeBlocks++
  return string(c.text), nil
}

Value <- val:( Number / Bool / Null / String / Array / Object / Pipe) {
    return val, nil
}

///////////////////// Number /////////////////////

Number <- Integer ( '.' DecimalDigit+ )? Exponent? {
  node, err := ast.NewNumberNode(
    string(c.text), c.pos.offset, c.pos.line, c.pos.col,
  )
	return node, err
}

Integer <- [+-]? DecimalDigit+ {
  return strconv.Atoi(string(c.text))
}

Exponent <- 'e'i [+-]? DecimalDigit+

DecimalDigit <- [0-9]

HexDigit <- [0-9a-f]i

///////////////////// Boolean /////////////////////

Bool <- ( "true" / "false") {
  cntCodeBlocks++
  return ast.NewBooleanNode(string(c.text), c.pos.offset, c.pos.line, c.pos.col)
}

///////////////////// Null /////////////////////

Null <- ( "null" ) {
  cntCodeBlocks++
  return ast.NewNullNode(string(c.text), c.pos.offset, c.pos.line, c.pos.col)
}

///////////////////// String /////////////////////

String <- DoubleQuoteString / SingleQuoteString

DoubleQuoteString <- '"' ( !EscapedChar . / '\\' EscapeSequence )* '"' {
    c.text = bytes.Replace(c.text, []byte(`\/`), []byte(`/`), -1)
    value, _ := strconv.Unquote(string(c.text))
    return ast.NewStringNode(value, c.pos.offset, c.pos.line, c.pos.col)
}

SingleQuoteString <- "'" str:PlainString "'" {
    return ast.NewStringNode(string(c.text), c.pos.offset, c.pos.line, c.pos.col)
}

PlainString <- ( !EscapedChar . / '\\' EscapeSequence )* {
    c.text = bytes.Replace(c.text, []byte(`\/`), []byte(`/`), -1)
    return string(c.text), nil
}

EscapedChar <- [\x00-\x1f"'\\]

EscapeSequence <- SingleCharEscape / UnicodeEscape

SingleCharEscape <- ["\\/bfnrt]

UnicodeEscape <- 'u' HexDigit HexDigit HexDigit HexDigit

////////////////////// Array //////////////////////

Array <- '[' _ vals:( Expr ( ',' _ Expr )* )? ']' {
    return parseArray(vals, c.text, c.pos.offset, c.pos.line, c.pos.col)
}

Object <- '{' _ vals:( String _ ':' _ Expr ( ',' _ String _ ':' _ Expr )* )? '}' {
    return parseObject(vals, c.text, c.pos.offset, c.pos.line, c.pos.col)
}

////////////////////// Whitespace //////////////////////

_ "whitespace" <- [ \n\t\r]*

////////////////////// EOF //////////////////////

EOF <- !.
